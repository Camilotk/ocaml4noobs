# Controle e Organiza√ß√£o

At√© aqui utilizamos bastante o REPL para fazer experimentos e aprender a usar a linguagem, mas desse cap√≠tulo em diante vamos come√ßar a focar um pouco mais em trabalhar com arquivos e projetos e come√ßar a construir programas. Arquivos s√£o mais do que apenas uma forma de armazenar nossas instru√ß√µes para serem compiladas ou interpretadas, em OCaml os arquivos tamb√©m correspondem a **m√≥dulos** que dividem nossos programas em unidades conceituais de c√≥digo.

#### **Ind√≠ce**
- [Fun√ß√£o Main](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#fun%C3%A7%C3%A3o-main)
- [M√≥dulos e Assinaturas](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#m%C3%B3dulos-e-assinaturas)
    - [Assinaturas e Interfaces](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#assinaturas-e-interfaces)
    - [Tipos Abstratos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#tipos-abstratos)
    - [Subm√≥dulos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#subm%C3%B3dulos)
    - [Abrindo M√≥dulos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#abrindo-m%C3%B3dulos)
    - [Extendendo M√≥dulos e Assinaturas](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#abrindo-m%C3%B3dulos)
- [Erros Comuns com M√≥dulos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#erros-comuns-com-m%C3%B3dulos)
- [Projetando M√≥dulos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#projetando-m%C3%B3dulos)
    - [Raramente exponha tipos concretos](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#raramente-exponha-tipos-concretos)
    - [Projete para quem est√° chamando](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#projete-para-quem-est%C3%A1-chamando)
    - [Crie interfaces uniformes](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#crie-interfaces-uniformes)
    - [Interface antes da implementa√ß√£o](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#interface-antes-da-implementa%C3%A7%C3%A3o)
- [Functors](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#functors)
- [M√≥dulos de primeira-classe](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#m%C3%B3dulos-de-primeira-classe)
    - [Construindo m√≥dulos de Primeira Classe](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#m%C3%B3dulos-de-primeira-classe)
    - [Acessando m√≥dulos de primeira classe](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#acessando-m%C3%B3dulos-de-primeira-classe)
    - [Voc√™ pode viver sem m√≥dulos de primeira classe](https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#voc%C3%AA-pode-viver-sem-m%C3%B3dulos-de-primeira-classe)

## Fun√ß√£o Main

Diferente de C ou Java n√£o temos uma fun√ß√£o chamada **main** por onde nossos programas come√ßam a execu√ß√£o. Quando um programa OCaml √© executado todas as suas instru√ß√µes s√£o avaliadas em execu√ß√£o. Na maioria dos scripts e programas que criamos a declara√ß√£o **let () =** faz o papel da fun√ß√£o main, mas ainda assim devemos ter a consci√™ncia que todo nosso arquivo √© avaliado em execu√ß√£o e ent√£o de alguma forma todos os arquivos em OCaml s√£o uma grande fun√ß√£o main.

Parece um pouco estranho escrever **let () =**, mas isso tem um sentido. A nossa atribui√ß√£o **let** faz pattern match o valor **unit**, que garante que a express√£o ap√≥s **=** retorne um valor igualmente unit.


## M√≥dulos e Assinaturas

M√≥dulos s√£o como **mini arquivos**. Eles podem agregar diversas defini√ß√µes como tipos, valores, constantes, outros m√≥dulos e todas as demais coisas que podemos declarar em um arquivo.

Todo arquivo OCaml em si √© compilado para um m√≥dulo com o mesmo nome do arquivo, mas capitalizado (com a sua inicial mai√∫scula). Se tivermos um arquivo ```ocaml.ml``` todo seu conte√∫do forma um m√≥dulo chamado **Ocaml** que pode ser acessado por qualquer outro arquivo. Os conteudos de um m√≥dulo podem ser acessados por ponto **.** assim como as propriedades de um record. 

> Por conven√ß√£o geralmente utilizamos nomes de arquivos inteiramente em letras min√∫sculas, nomes capitalizados n√£o causam qualquer tipo de erro ou s√£o inv√°lidos, mas dificilmente encontra-se programadores que utilizam essa conven√ß√£o. Independente da conven√ß√£o, o nome do m√≥dulo ainda √© o nome capitalizado do arquivo.

Quando escrevemos um programa OCaml que roda inteiramente no mesmo arquivo todos seus valores, fun√ß√µes e estruturas est√£o interagindo dentro desse m√≥dulo que √© representado pelo nome do arquivo.

### Assinaturas e Interfaces

Quando escrevemos nosso c√≥digo OCaml definimos nossas express√µes da linguagem em um arquivo ```.ml``` que como vimos define implicitamente um m√≥dulo, mas digamos que queremos que um programador possa usar nossa classe sem saber necessariamente a implementa√ß√£o interna de nosso c√≥digo e para essa situa√ß√£o usamos os arquivos ```.mli``` que definem **interfaces**.

> Assim como todo arquivo .ml √© implicitamente um m√≥dulo, todo arquivo .mli √© implicitamente uma interface.

Para isso, temos que definir uma interface de m√≥dulo, que atuar√° como uma m√°scara sobre a implementa√ß√£o do m√≥dulo. Para definirmos a interface de um m√≥dulo representado pelo arquivo ```module.ml``` devemos criar um arquivo ```module.mli``` na mesma pasta, tanto o compilador padr√£o de OCaml, quanto dune sempre procuram por um arquivo .mli de mesmo nome que o arquivo que est√£o compilando e caso n√£o o encontre v√£o inferir a interface para o m√≥dulo.

> Junto com o resultado de toda compila√ß√£o temos um arquivo com a extens√£o ```.cmi``` que justamente se trata do resultado de compila√ß√£o da interface do m√≥dulo. Note que mesmo quando n√£o definimos explicitamente um arquivo ```.mli``` esse arquivo est√° presente, justamente por que o compilador vai inferir uma interface para tudo que est√° declarado no arquivo e expor todas as express√µes e valores do mesmo.

Vendo isso de uma forma mais pr√°tica, vamos come√ßar definindo um arquivo (m√≥dulo) apenas de exemplo chamado **moduloa.ml**:
```ocaml
let mensagem = "Que bom que voc√™ est√° aprendendo OCaml!"

let imprime () = print_endline mensagem;;
```
E digamos que n√£o queremos que os m√≥dulos que v√£o acessar esse tenham acesso a nossa declara√ß√£o **mensagem** porque eles devem apenas acessar a nossa fun√ß√£o **imprime**, ent√£o vamos criar o nosso arquivo ```.mli``` com o mesmo nome, na mesma pasta ou seja **moduloa.mli**:
```ocaml
val imprime : unit -> unit
```

Voc√™ pode expor qualquer valor ou fun√ß√£o utilizando a palavra **val** seguida da sua assinatura (como o REPL mostra quando entramos express√µes). Nesse caso quando compilarmos nosso **moduloa.ml** quando algu√©m abri-lo e tentar acessar apenas ter√° acesso a **imprime**.

> Podemos usar o compilador de OCaml para inferir as assinaturas do nosso arquivo .ml para n√≥s, para isso usamos ```ocamlc -i <nome do arquivo>```.

Importante saber que quando usamos dune para compilar nossos arquivos n√£o precisamos nos preocupar pois ele vai cuidar dos arquivos .mli para n√≥s, mas quando usamos o compilador de ocaml devemos sempre especificar a compila√ß√£o dos arquivos .mli antes dos seus respectivos .ml por ex:

```
$ ocamlc moduloa.mli moduloa.ml
```

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

### Tipos Abstratos

N√£o incomum que nossos m√≥dulos em OCaml contenham a defini√ß√£o de novos tipos. E como funciona a exposi√ß√£o de novos tipos? Bem para isso vamos imaginar que estamos criando agora um arquivo **modulob.ml** que vai conter um novo tipo data:

```ocaml
type data = {dia: int; mes: int; ano: int}
```

Quando formos escrever nosso **modulob.mli** vamos ter quatro op√ß√µes de como expor isso na interface no nosso arquivo ```.mli```:

1. Omitindo completamente o tipo da assinatura.
2. Copiando a defini√ß√£o do tipo na declara√ß√£o da assinatura.
3. O tipo √© transformado em abstrato e apenas seu nome √© declarado.
4. Os campos desse record s√£o transformados em apenas leitura.

Nesse caso vamos estudar a op√ß√£o **3** que vai ser declarada em nosso arquivo assim:
```ocaml
type data
```
Agora os usu√°rios do m√≥dulo **Modulob** v√£o ser capazes de manipular express√µes do tipo **date**, mas sem ter acesso aos campos desse record diretamente. Eles precisam usar as fun√ß√µes desse tipo que o m√≥dulo cont√™m. Vamos assumir que esse m√≥dulo cont√™m tr√™s fun√ß√µes, uma para criar um valor de data, outra para computar a dist√¢ncia entre duas datas e a outra que retorna a data em ano:

```ocaml
type data

val nova : int -> int -> int -> data

val diferenca : data -> data -> data

val anos : data -> float
```

Agora apenas as fun√ß√µes **nova** e **diferenca** podem criar valores do tipo data assim garantindo que n√£o vamos ter records que quebrem a forma√ß√£o esperada desse tipo. Isso vai ajudar a garantir que nada que dependa de um valor desse tipo vai quebrar por conta disso. Isso faz muito sentido em uma biblioteca j√° que as vers√µes subsequentes dessa biblioteca podem continuar expondo a mesma interface, enquanto internamente a implementa√ß√£o continue mudando inclusive as defini√ß√µes de tipo.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

### Subm√≥dulos

M√≥dulos podem tamb√©m conter m√≥dulos, quando temos um m√≥dulo dentro de outro chamamos essa estrutura de subm√≥dulos (submodules) ou m√≥dulos aninhados (nested modules). Quando declaramos um m√≥dulo em um arquivo, este por sua vez automaticamente torna-se um subm√≥dulo do mesmo.

A sintaxe b√°sica para declarar um m√≥dulo √©:
```ocaml
module Nome = struct
    (* valores e express√µes*)
end
```

Para melhor exemplificar imagine que temos um arquivo chamado ```twitch.ml``` e nele vamos declarar um submodulo:
```ocaml
module Eduardo = struct
    let link = "https://www.twitch.tv/eduardorfs"
    let imprime () = Printf.printf "Voc√™ pode ver lives de OCaml em %s \n" link
end

let link_emelle () = Printf.printf "Voc√™ pode ver entrevistas sobre OCaml em %s \n" "https://www.twitch.tv/emelletv";

let lista_links () =
    Eduardo.imprime ();
    link_emelle ()
```

Se tentarmos acessar essas declara√ß√µes de outro m√≥dulo teremos dois n√≠veis de m√≥dulos. Podemos escrever assim:
```OCaml
let () =
    Twitch.Eduardo.imprime ();
    Twitch.link_emelle ()
```

Podemos restringir a interface de um determinado submodulo declarado em nosso arquivo. Isso √© normalmente chamado de tipo de m√≥dulo (module type). A estrutura b√°sica para isso √©

```ocaml
module Nome = sig
    (* Aqui v√£o as assinaturas *)
end
=
struct
    (* Aqui v√£o os valores *)
end
```

Vamos aplicar isso no nosso arquivo ```twitch.ml```:
```ocaml
module Eduardo =  sig
  val imprime : unit -> unit
end 
= 
struct
  let link = "https://www.twitch.tv/eduardorfs"
  let imprime () = Printf.printf "Voc√™ pode ver lives de OCaml em %s \n" link
end

(* Agora link n√£o √© mais acess√≠vel a partir daqui *)

let link_emelle () = Printf.printf "Voc√™ pode ver entrevistas sobre OCaml em %s \n" "https://www.twitch.tv/emelletv";

let lista_links () =
  Eduardo.imprime ();
  link_emelle ()
```

A defini√ß√£o do nosso m√≥dulo ```Eduardo``` acima √© equivalente a um par de arquivos ```eduardo.ml``` e ```eduardo.mli``` . Escrever isso em um √∫nico bloco de c√≥digo nem sempre √© elegante, √© poss√≠vel escrever a assinatura e as defini√ß√µes em blocos separados:

```ocaml
module type Eduardo_type = sig
    val imprime : unit -> unit 
end

module Eduardo : Eduardo_type = struct
    (* ... *)
end
```

A vantagem de usarmos blocos separados para declarar nossas interfaces √© que elas podem ser reaproveitadas em mais de um m√≥dulo.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

### Abrindo M√≥dulos

A maior parte do tempo nos referimos aos valores, fun√ß√µes e tipos em um m√≥dulo usando o acesso qualificado colocando o nome do m√≥dulo na frente do valor que est√° sendo acessado. Por exemplo vamos escrever ```List.map``` para nos referimos a fun√ß√£o. Por√©m quando quisermos nos referir a fun√ß√µes em um m√≥dulo sem seu identificador usamos a palavra reservada **open**.

> A raz√£o para preferirmos o acesso qualificado especialmente no m√≥dulo List √© que isso nos ajuda a evitar conflito com valores de outros m√≥dulos que tenham o mesmo identificador, como √© o caso do m√≥dulo Array que tamb√©m declara a fun√ß√£o map.

Por exemplo, se tivermos dois arquivos um **sum.ml** que contem uma simples fun√ß√£o de soma chamada ```add```: 

**sum.ml**
```ocaml
let add x y = x + y
```

E quisermos cham√°-la no nosso arquivo **main.ml** podemos tanto utilizar o acesso qualificado:

**main.ml**
```ocaml
let () = 
    let result = Sum.add 2 3 in
    print_endline (string_of_int result);
```

Quanto podemos abrir o m√≥dulo e ent√£o usar todas as defini√ß√µes presentes em **sum.ml** sem necessitar da qualifica√ß√£o:

**main.ml**
```ocaml
open Sum

let () = 
    let result = add 2 3 in
    print_endline (string_of_int result);
```

Isso parece trivial com o exemplo sendo t√£o simples, mas a medida que nossos programas crescem come√ßa a tornar-se mais √∫til utilizar **open**. Abrir um m√≥dulo √© um trade-off (uma troca) entre ter uma escrita tersa para utilizar as express√µes de um m√≥dulo e ser expl√≠cito em de onde essas express√µes vem e como elas se relacionam. Quanto mais m√≥dulos voc√™ abre, mais limpo o c√≥digo fica, por√©m pode ser mais dif√≠cil de ler o c√≥digo e saber de onde vem cada coisa.

Uma outra forma de abrir m√≥dulos que fica no meio termo entre essa troca √© a abertura local, a principal diferen√ßa desse tipo de **open** √© que ele √© restrito ao escopo e existem duas formas de fazer.

A primeira √© utilizando **in**:
```OCaml
let () = 
    let open Sum in
    let result = add 2 3 in
    print_endline (string_of_int result);
```

A outra ainda mais restrita que utilizando **in** pois restringe o escopo dentro de par√™nteses e √© muito √∫til quando temos pequenas express√µes que precisamos.
```OCaml
let () = 
    let result = Sum.(add 2 3) in
    print_endline (string_of_int result);
```

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

### Extendendo M√≥dulos e Assinaturas

Quando trabalhamos com m√≥dulos podemos criar novos m√≥dulos que estendem os preexistentes, ou seja, eles recebem todas as caracter√≠sticas declaradas no outro m√≥dulo mais as que o usu√°rio define, isso √© pr√≥ximo do conceito de "heran√ßa" ou "mixin" em outras linguagens.

> O uso de **include** √© desaconselhado pois gera duplica√ß√£o de c√≥digo como copy-paste em um n√≠vel de compila√ß√£o.

Digamos que estamos usando o m√≥dulo presente no ambiente padr√£o de OCaml **List**, mas o que realmente queremos √© criar uma fun√ß√£o que fa√ßa parte de List, para isso criamos um arquivo ```extension.ml``` onde vamos usar a palavra reservada **include** para incluir a defini√ß√£o do m√≥dulo List no nosso novo modulo List e declarar a nossa fun√ß√£o junto a esse:
```ocaml
module List = struct
    include List
    let rec optmap f = function
      | [] -> []
      | hd :: tl ->
         match f hd with
         | None -> optmap f tl
         | Some x -> x :: optmap f tl
  end;;
```

Isso vai criar um m√≥dulo ```Extension.List``` que vai ter um m√≥dulo que cont√™m tudo o que tem no m√≥dulo base de **List** e mais a fun√ß√£o **optmap**. De outro arquivo podemos abrir o m√≥dulo Extension e utilizar a nova defini√ß√£o de List:

```OCaml
open Extensions

...

List.optmap ...
```

Assim como usamos a palavra **include** para estender m√≥dulos, tamb√©m podemos us√°-la para estender assinaturas:
```ocaml
module type Pessoa = sig
    val nome : string
    val altura : float
end

module type Pessoa_com_idade = sig
    include A
    val idade : int
end
```

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

## Erros Comuns com M√≥dulos

Quando OCaml compila um programa com uma extens√£o ```ml``` e um com a extens√£o ```mli``` vai haver erros caso haja algo fora de lugar do esperado com um ou com outro. Na maioria das vezes ser√° um dos erros a seguir.

- **Type Mismatch**: O erro mais comum de acontecer. Acontece quando alguma das assinaturas no arquivo mli n√£o bate com a implementa√ß√£o declarada no arquivo ml.

- **Missing Definition**: Acontece quando compilamos ambos arquivos mli e ml, por√©m falta uma das implementa√ß√µes definidas nas assinaturas do arquivo mli no arquivo ml.

- **Type Definition Mismatch**: Os tipos declarados no arquivo mli n√£o est√£o batendo com os tipos da implementa√ß√£o no arquivo ml.

- **Cyclic Dependencies**: OCaml n√£o permite depend√™ncias ciclicas (quando duas defini√ß√µes dependem uma da outra mutuamente). Caso haja depend√™ncia c√≠clica entre dois m√≥dulos isso ir√° causar um erro.

## Projetando M√≥dulos

O sistema de m√≥dulos √© uma das principais partes da arquitetura de qualquer programa OCaml e por isso √© necess√°rio que sejam projetados da forma mais eficiente poss√≠vel. Vamos ver alguns conceitos que v√£o ajudar a projetar programas utilizando o sistema de m√≥dulos.

### Raramente exponha tipos concretos

Quando estiver estruturando um arquivo .mli, uma das escolhas que voc√™ precisa fazer √© expor ou n√£o a defini√ß√£o concreta dos tipos em seu m√≥dulo ou expor eles como tipos abstratos. Na maioria das vezes, tipos abstratos √© a escolha correta por duas raz√µes:

1. Tipos abstratos aumentam a flexibilidade do seu c√≥digo.
2. Tipos abstratos permitem impor como o tipo deve usado.

Abstra√ß√£o aumenta a flexibilidade do c√≥digo restringindo como os usu√°rios do seu m√≥dulo v√£o interagir com os tipos, reduzindo o n√∫mero de formas que os usu√°rios podem criar depend√™ncias com a implementa√ß√£o. Se voc√™ optar por expor de forma expl√≠cita os tipos, os usu√°rios podem depender de qualquer e todo detalhe que est√° exposto Se voc√™ exp√¥-los de forma abstrata ent√£o apenas as opera√ß√µes espec√≠ficas expostas ser√£o dispon√≠veis. Isso significa que voc√™ pode tranquilamente mudar a implementa√ß√£o sem afetar o que o cliente est√° consumindo enquanto voc√™ preservar as defini√ß√µes.

### Projete para quem est√° chamando

Quando estiver escrevendo uma interface voc√™ deve considerar o qu√£o ser√° f√°cil para algu√©m entender essa interface quando estiver lendo o seu arquivo mli. Voc√™ quer que cada chamada tenha uma assinatura √≥bvia para quem vai consumir essas express√µes sejam elas tipos, valores ou fun√ß√µes.

A raz√£o para isso na maioria das vezes √© que as pessoas interagindo com a sua API v√£o estar lendo e modificando o pr√≥prio c√≥digo e n√£o lendo a defini√ß√£o da interface. Fazendo a API ser o mais √≥bvia o poss√≠vel por essa perspectiva voc√™ est√° facilitando para as pessoas que est√£o consumindo.

Voc√™ pode fazer isso por exemplo escolhendo bons nomes descritivos para suas fun√ß√µes, tipos variantes e records. Bons nomes geralmente n√£o precisam ser longu√≠ssimos para serem claros. Por exemplo √© perfeitamente claro usar x para nomear vari√°veis em fun√ß√µes an√¥nimas ex: ```(fun x -> x *. x)```. Uma boa regra para se seguir quando est√° criando o nome de fun√ß√µes para seu programa √© que quanto menor o escopo, menor o nome e quanto mais global mais longo e expl√≠cito o nome precisa ser.

Outra dica √© que quanto menos usado um valor √©, maior vantagem em utilizar nomes expl√≠citos e verbosos, uma vez em que o custo de ser verboso diminui e a vantagem de ser explicito aumenta.

### Crie interfaces uniformes

Quando estamos desenvolvendo uma interface n√£o devemos pens√°-la de forma isolada. Normalmente nossas interfaces representam valores que interagem com o c√≥digo e com outras interfaces e isso deve funcionar de forma natural. Normalmente isso torna-se um trabalho mais f√°cil quando utilizamos conven√ß√µes ou padr√µes de interface, o que torna mais f√°cil consumi-las e trabalhar com elas.

Algumas conven√ß√µes bastante usadas:

- Um m√≥dulo para *praticamente* todo tipo na nossa aplica√ß√£o. Normalmente constru√≠mos um m√≥dulo para cada tipo que definimos na nossa aplica√ß√£o e exportamos o tipo prim√°rio do m√≥dulo com o nome **t**.

- *Sempre colocamos **t** primeiro*. Se o nosso m√≥dulo hipot√©tico chamado ```M``` no qual o tipo prim√°rio √© ```M.t``` possui fun√ß√µes que usam valor do tipo ```M.t``` colocamos sempre esses valor como o primeiro.

Tamb√©m existem padr√µes e conven√ß√µes sobre fun√ß√µes comumente utilizadas e reescritas frequentemente em m√≥dulos, como a fun√ß√£o map. A assinatura de map por conven√ß√£o sempre utiliza tipos gen√©ricos por conven√ß√£o, isso ajuda a termos uma maior uniformidade, mas esse tipo de conven√ß√£o espec√≠fica voc√™ consegue aprender com experi√™ncia a medida em que acumular pr√°tica consumindo interfaces. No momento concentre-se em aprender as duas conven√ß√µes acima.

### Interface antes da implementa√ß√£o

OCaml √© uma linguagem muito concisa e que possui muita flexibilidade com tipos e design de c√≥digo. Uma das principais tarefas de um programa OCaml bem escrito √© a implementa√ß√£o utilizar de forma correta e segura tipos, por isso √© importante que a gente pense os tipos que nosso programa usa com cuidado.

Um bom m√©todo para isso √© escrever as defini√ß√µes de tipo antes da implementa√ß√£o da l√≥gica, tanto escrevendo primeiro a assinatura quando estamos trabalhando com subm√≥dulos quanto escrevendo os nossos arquivos ```.mli``` antes dos ```.ml```.

Claro que voc√™ n√£o precisa ser estritamente r√≠gido com essa regra, √© normal por vezes que seja mais pr√°tico escrever primeiro a l√≥gica, principalmente quando estamos prototipando coisas onde nossos par√¢metros e valores n√£o s√£o bem definidos. Mas tipos e assinaturas s√£o as ferramentas mais importantes que OCaml nos prov√™ para construir a estrutura do nosso c√≥digo de uma forma que seja clara o que estamos buscando atingir com nosso c√≥digo.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

## Functors

M√≥dulos podem ser passados como fun√ß√µes. Isso √© equivalente ao conceito de fun√ß√µes de primeira classe que vimos no cap√≠tulo sobre fun√ß√µes. Por√©m, m√≥dulos s√£o intrinsecamente diferentes de fun√ß√µes normais. Ao inv√©s de passarmos eles como fun√ß√µes normais, n√≥s utilizamos fun√ß√µes especiais chamadas **Functors**.

> Na matem√°tica, mais precisamente teoria das categorias, um functor ou funtor √© um mapeamento entre categorias, preservando dom√≠nios, contradom√≠nios, identidades e composi√ß√µes, analogamente a como, por exemplo, um homomorfismo de grupos preserva o elemento neutro e a opera√ß√£o do grupo.

A principal diferen√ßa que temos nos functors √© que essas fun√ß√µes recebem m√≥dulos como par√¢metros e tamb√©m retornam m√≥dulos, somente. Functors nos permitem criar m√≥dulos parametrizados e prover um m√≥dulo a outro que recebe a implementa√ß√£o. 

Digamos que temos uma assinatura do m√≥dulo ```Stringable``` definida, sendo que essa assinatura define a exposi√ß√£o de um tipo abstrato **t** e uma fun√ß√£o **toString** que recebe um valor **t** e retorna uma string:

```ocaml
module type Stringable  = sig 
    type t 
    val toString : t -> string 
end

```

E queremos criar um functor que vai receber essa assinatura e utilizar os tipos definidos nessa interface para declarar duas fun√ß√µes parametrizadas que v√£o utilizar os tipos dessa interface:

```ocaml
module Printer (Item:Stringable) = struct
    let print (t : Item.t) = print_endline (Item.toString t)
    let printList (list : Item.t list) =
      ((list |> (List.map Item.toString)) |>
         (String.concat ((", ") )))
        |> print_endline
end
```

Agora podemos usar esse functor passando um m√≥dulo que implementa essa interface como par√¢metro e passando as implementa√ß√µes concretas desse tipo:
```ocaml
module IntPrinter = Printer (struct 
    type t = int
    let toString = string_of_int end)
```
O functor ir√° nos retornar uma implementa√ß√£o concreta de um m√≥dulo que ser√° atribu√≠da para o m√≥dulo **IntPrinter** que al√©m de conter os tipos do m√≥dulo passado como par√¢metro para o functor tamb√©m vai conter as fun√ß√µes que foram declaradas param√©tricamente em sua defini√ß√£o.
```ocaml
IntPrinter.printList [1; 2; 3];;
(* 1, 2, 3 
   - : unit = () *)

IntPrinter.print 10;;
(* 10
   - : unit = () *)
```

E agora podemos usar esse functor para gerar a implementa√ß√£o de outros m√≥dulos que implementam a mesma assinatura:
```ocaml
module FloatPrinter = Printer (struct 
    type t = float
    let toString = string_of_float end)

FloatPrinter.printList [1.3; 5.6; 9.7];;
(* 1.3, 5.6, 9.7
   - : unit = () *)
```

Obviamente, o functor assim como o restante em OCaml tem uma forte checagem para garantir que o m√≥dulo passado corresponde a implementa√ß√£o da interface usada como par√¢metro e se tentarmos passar um m√≥dulo que n√£o atenda essa defini√ß√£o: 
```ocaml
 module FalsePrinter = Printer (struct 
      let toString = 1 end);;
```

Teremos um erro:
```
Error: Signature mismatch:
       Modules do not match:
        sig val toString : int end
       is not included in
        Stringable
       The type `t' is required but not provided
```

Functors nos ajudam a parametrizar nossas aplica√ß√µes e modelar melhores m√≥dulos, al√©m de permitir um grande reaproveitamento de c√≥digo us√°-los da forma correta pode ser √∫til para resolver v√°rios problemas de estrutura de c√≥digo incluindo:

- **Inje√ß√£o de depend√™ncia** Pois fazem que a implementa√ß√£o de alguns componentes sejam cambi√°veis. Isso √© particularmente √∫til quando a inten√ß√£o √© prototipar partes do sistema para testes e simula√ß√µes.

- **Autoextens√£o de m√≥dulos** Functors nos proveem uma forma de estender nossos m√≥dulos com novas funcionalidades de uma forma padronizada. Functors nos permitem escrever a l√≥gica apenas uma vez e reaplic√°-la e como vimos anteriormente podemos usar **include** para fazer isso, mas certamente functors podem nos prover isso de forma mais organizada e eficiente.

- **Instanciar m√≥dulos com estado** M√≥dulos podem conter estados mut√°veis, isso significa que algumas vezes pode ser √∫til ter m√∫ltiplas inst√¢ncias de um m√≥dulo em particular, cada um com seu estado individual. Functors nos permitem automatizar a constru√ß√£o desses m√≥dulos.

Essas s√£o algumas dessas aplica√ß√µes, existem muitas outras que s√£o possibilitadas com a utiliza√ß√£o de Functors e que voc√™ pode encontrar em codebases OCaml.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

## M√≥dulos de primeira-classe

Voc√™ pode pensar no OCaml como sendo dividido em duas partes: uma linguagem que se preocupa com valores e tipos e uma linguagem que se preocupa com m√≥dulos e assinaturas de m√≥dulo. Essas sublinguagens s√£o separadas, em que os m√≥dulos podem conter tipos e valores, mas os valores comuns n√£o podem conter m√≥dulos ou tipos de m√≥dulo. Isso significa que voc√™ n√£o pode fazer coisas como definir uma vari√°vel cujo valor √© um m√≥dulo ou uma fun√ß√£o que recebe um m√≥dulo como argumento.

OCaml fornece uma maneira de contornar essa separa√ß√£o na forma de m√≥dulos de primeira classe. M√≥dulos de primeira classe s√£o valores comuns que podem ser criados e convertidos de volta em m√≥dulos regulares.

M√≥dulos de primeira classe s√£o uma t√©cnica sofisticada e voc√™ precisar√° se familiarizar com alguns aspectos avan√ßados da linguagem para us√°-los de forma eficaz. Mas vale a pena aprender, porque deixar os m√≥dulos na linguagem central √© bastante poderoso, aumentando o alcance do que voc√™ pode expressar e tornando mais f√°cil construir sistemas flex√≠veis e modulares.

### Construindo m√≥dulos de Primeira Classe

Para melhor entender esse conceito, digamos que temos uma assinatura de um m√≥dulo que apenas contem um tipo inteiro:
```ocaml
module type IntValue = sig
    val value : int
end
```
N√≥s podemos criar um m√≥dulo que implemente essa assinatura:
```ocaml
module Tres : IntValue = struct
    let value = 3
end
```

Para construir um m√≥dulo de primeira classe devemos empacotar o m√≥dulo e sua assinatura. Isso √© feito utilizando a palavra reservada **module** nessa estrutura:
```
(module <Nome> :  <Tipo>)
```

Por exemplo para nosso exemplo que definimos acima isso seria feito da seguinte forma:
```ocaml
let tres = (module Tres : IntValue);;
(* val tres : (module IntValue) = <module> *)
```

O tipo do m√≥dulo √© opcional na constru√ß√£o de um m√≥dulo de primeira classe quando ele pode ser inferido pelo conte√∫do:
```ocaml
module Quatro = struct let value = 4 end ;;
(* module Quatro : sig val value : int end *)
```

Quando empacotamos m√≥dulos podemos utiliz√°-los como valores, por exemplo em listas:
```ocaml
let numeros = [tres; (module Quatro)];;
(* val numeros : (module IntValue) list = [<module>; <module>] *)
```

Tamb√©m podemos usar **m√≥dulos an√¥nimos** para criar m√≥dulos de primeira classe, podemos definir um m√≥dulo an√¥nimos em OCaml utilizamos a estrutura:
```
(module struct <let...> end)
```
Por exemplo:
```ocaml
let numeros = [tres; (module struct let value = 5 end)] ;;
(* val numeros : (module IntValue) list = [<module>; <module>] *)
```
> Observe que foi inferido que o m√≥dulo an√¥nimo na lista trata-se de uma implementa√ß√£o de **IntValue** pelo compilador. Se tent√°ssemos declarar como um m√≥dulo que n√£o h√° assinatura, ter√≠amos um erro.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

### Acessando m√≥dulos de primeira classe

Podemos acessar o conte√∫do de m√≥dulos de primeira classe quando os desempacotamos em m√≥dulos normais. Para fazer isso usamos a palavra reservada **val** com a seguinte estrutura:
```
(val <Modulo> : <Tipo do modulo>)
```
Por exemplo:
```ocaml
module Outro_tres = (val tres : IntValue) ;;
(* module Outro_tres : IntValue *)

Outro_tres.value ;;
(* - : int = 3 *)
```

N√≥s tamb√©m podemos escrever fun√ß√µes que consomem e produzem m√≥dulos de primeira classe. 

```ocaml
let to_int input_module =
  let module M = (val input_module : IntValue) in
  M.value
(* val to_int : (module IntValue) -> int = <fun> *)

let soma first_module second_module = 
    (module struct 
        let value = to_int first_module + to_int second_module
    end : IntValue)
(* val soma : (module IntValue) -> (module IntValue) -> (module IntValue) = <fun> *)
```

Agora podemos usar essas fun√ß√µes para trabalhar definindo novos m√≥dulos:
```ocaml
let seis = soma tres tres;;
(* val seis : (module IntValue) = <module> *)
```

E podemos perceber que quando desempacotarmos nosso m√≥dulo seu valor passa a ser o retornado pela nossa fun√ß√£o.

### Voc√™ pode viver sem m√≥dulos de primeira classe

M√≥dulos de primeira classe √© um conceito avan√ßado da linguagem e ele serve como atalho e uma forma de facilitar a solu√ß√£o de problemas espec√≠ficos. Por√©m √© importante que voc√™ saiba que tudo que pode ser feito usando m√≥dulos de primeira classe, pode ser feito sem usar m√≥dulos de primeira classe.

Especialmente quando estamos trabalhando em projetos pequenos e/ou de baixa complexidade talvez eles nem mesmo sejam necess√°rios de nenhuma forma. Por isso sempre leve em considera√ß√£o quando estiver escrevendo seus m√≥dulos o trade off de ganho x aumento de complexidade que t√©cnicas como essa oferecem.

<p align="right"><a href="https://github.com/Camilotk/ocaml4noobs/tree/master/4%20-%20organizacao#ind%C3%ADce">üîù Subir para o topo</a></p>

## Materiais Complementares e Refer√™ncias
- [Ponto de entrada](https://translate.google.com/translate?sl=en&tl=pt&u=https://en.wikipedia.org/wiki/Entry_point%23OCaml)
- [Functor](https://pt.wikipedia.org/wiki/Functor)
- [Cidad√µes de primeira classe](https://pt.wikipedia.org/wiki/Cidad%C3%A3o_de_primeira_classe)
- [Fun√ß√£o de primeira classe](https://pt.wikipedia.org/wiki/Fun%C3%A7%C3%A3o_de_primeira_classe)
- [Diferen√ßa de functors em Haskell e OCaml](https://try2explore.com/questions/pt/10341180)

#### E por enquanto, √© s√≥ nos vemos no pr√≥ximo [Cap√≠tulo](https://github.com/Camilotk/ocaml4noobs/tree/master/5%20-%20tipos)! üòâ
