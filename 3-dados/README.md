# Dados
Depois de fun√ß√µes, a coisa mais importante para se entender sobre OCaml (e outras linguagens similares) √© dados. Nesse Cap√≠tulo vamos dar uma olhada nos tipos de dados e suas extens√µes em OCaml e entender como trabalhar com eles e definir nossos pr√≥prios tipos.

## Tipos Pr√©-definidos
A bilblioteca padr√£o de OCaml nos traz tipos pr√©-definidos (que tamb√©m podem ser chamados de primitivos) em sua biblioteca padr√£o. Esses tipos s√£o as formas mais b√°sicas (ou se preferir at√¥micas) de representa√ß√£o de um dado, s√£o as unidades que s√£o usadas para compor novos valores e tipos.

### Tipos B√°sicos

Abaixo temos uma tabela dos tipos mais b√°sicos dos tipos pr√©-definidos em OCaml, sua escrita literal representa um exemplo de entrada usada para criar um valor do tipo descrito.

| Tipo       | Escrita Literal da Sintaxe              |
:--:         | :--:
| int        | 314                                     |
| float      | 3.14 ou 6.                              |
| char       | 'B' ou '\u0042'                         |
| string     | "Foo"                                   |
| bool       | true ou false                           |
| unit       | ()                                      |
| 'a list    | [1; 2]                                  |
| 'a array   | [| 1; 2 |]                              |
| tuple      | ("Cap√≠tulos", 3)                        |
| records    | { x = 5; y = 9 }                        |
| union      | type Dia = Segunda_Feira \| Terca_Feira |


## Int

OCaml tem suporte a inteiros de tamanho fixo de bytes, sendo o tamanho em bits do valor inteiro dois bits menor do que o inteiro nativo na m√°quina (62 bits na maioria dos computadores modernos). A biblioteca padr√£o tamb√©m possui m√≥dulos de inteiros com o tamanho fixado de inteiros para 32-bit e 64-bit que podem ser usados em qualquer m√°quina caso o programador tenha como alvo uma arquitetura espec√≠fica, apesar dos inteiros nesses m√≥dulos terem um tamanho maior eles s√£o √∫teis quando o programador precisa fazer comunica√ß√£o com outras linguagens como C.

A escrita literal de inteiros acontece da mesma forma que a sua escrita usual.

Ex: `1`, `2`, `-3`, `-34567`

O caractere "Underline" _ pode ser usado como separador (e ser√° ignorado) na escrita literal de n√∫meros grandes.

Ex: `19_474_489`, `545_690`

Os n√∫meros inteiros geralmente s√£o escritos na base 10 como padr√£o, mas tamb√©m podem ser escritos na base 2, 8 ou 16 normalmente como no exemplo abaixo.

`26` = `0b11010` = `0o32` = `0x1A`

> Cada um dos valores nas bases 2, 8 e 16 come√ßa com um `0` seguido do caractere que representa a flag da base que est√° sendo usada (`o` para octal, `b` para bin√°rio, `x` para hexadecimal) e eles podem ser usados em mai√∫scula tamb√©m (`O`, `B` e `X`).

## Float

Os valores de tipo `float`ou ponto flutuante em OCaml seguem o padr√£o definido no documento IEEE 754, possuem precis√£o de 64-bits. As opera√ß√µes com floats nunca causam exce√ß√µes quando acontece estouro, divis√£o por zero, etc. Ao inv√©s disso, caracteres pr√©-definidos no padr√£o IEEE s√£o retornados indicando o que aconteceu de acordo com o retorno, por exemplo se tentarmos fazer a divis√£o `1. /. 0.` no nosso REPL teremos como retorno `infinity`, teremos `-infinity` se for `-1. /. 0.` e teremos `nan` (Not a Number / N√£o √© um n√∫mero) se for `0.0 /. 0.0`.

> Observe que usamos `/.` ao inv√©s de `/` para dividir n√∫meros de ponto flutuante, isso acontece porque `/` √© uma fun√ß√£o que tem como par√¢metro dois inteiros enquanto `/.` tem como par√¢metro dois floats, o mesmo ocorre nas demais opera√ß√µes (`+.`, `-.`, `*.`).


## Convers√£o de Tipos Num√©ricos

Convers√£o de tipos √© algo importante quando estamos programando em OCaml, pois a infer√™ncia de tipos caracter√≠stica da linguagem √© capaz de fazer infer√™ncias inteligentes gra√ßas a sempre evitar fazer convers√µes impl√≠citas entre tipos. Se tentar passar um valor para um par√¢metro ou qualquer tipo de associa√ß√£o de valor e esse valor for diferente do esperado em nenhum momento haver√° uma tentativa de convers√£o para que isso funcione, isso ir√° retornar um erro. Isso significa que quando programamos em OCaml temos que constantemente fazer convers√µes expl√≠citas dos valores que estamos trabalhando e quando fazemos isso √© pressuposto que sabemos exatamente o que estamos fazendo, ent√£o caso seja feita convers√µes equivocadas isso pode causar perda de dados ou erros fatais na aplica√ß√£o.

Ex: Em PHP se tentarmos somar um inteiro e um n√∫mero de ponto flutuante isso ir√° funcionar.
```php
<?php
echo 1.0 + 3; 
// => 4
```
J√° em OCaml a fun√ß√£o de soma apenas ir√° aceitar dois par√¢metros float ou dois parametros int, mas nunca um de cada.
```ocaml
1 + 3;;
(* - : int = 4 *)

1. +. 3.;;
(* - : float = 4. *)

1 + 3.;;
(* Error: This expression has type float but an expression was expected of type int *)
```

Quando for necess√°rio, podemos converter valores utilizando as fun√ß√µes presentes na biblioteca padr√£o de OCaml.
```ocaml
float_of_int 3;;
(* - : float = 3. *)

int_of_float 3.1415;;
(* - : int = 3 *)

float_of_string "3.1415";;
(* - : float = 3.1415 *)
```

## Char

Os valores do tipo `char` ou caractere s√£o valores que representam apenas um caractere ASCII (mas existem bibliotecas que podem extender para Unicode como [Camomile](https://github.com/yoriyuki/Camomile)). A forma literal de escrita do tipo Char √© usando aspas simples usando barra invertida no formato UNIX.

Ex: `'\n'`, `'a'`

## String

Os valores to tipo `string` s√£o arrays de bytes imut√°veis e indexados por valores inteiros com tamanho de at√© 2<sup>57</sup>-9 (18 446 744 000 000 000 000) posi√ß√µes na arquitetura de 64-bit. A forma literal de escrita √© utilizando qualquer n√∫mero de caracteres Unicode delimitados por aspas duplas.

Ex: `""`, `" "`, `"foo"`, `"tab\ndata"`, `"ü¶ô"`

√â poss√≠vel quebrar linhas utilizando barra invertida seguida de espa√ßo para delimitar a quebra de linha.

Ex:
```ocaml
let haiku = "Relax, the future \
 is already unfolding \
 from long-planted seeds."
```

### Acessando carateres em uma String

Para acessarmos os caracteres em posi√ß√µes de uma String podemos usar a sintaxe de acesso `.[i]` passando o √≠ndice do caractere na String que queremos acessar.

```ocaml
"4noobs".[0];;
(* - : char = '4' *)

"OCaml".[3];;
(* - : char = 'm' *)
```

### M√≥dulo String

A biblioteca padr√£o de OCaml possui muitos m√≥dulos, entre eles os m√≥dulos que definem os tipos b√°sicos da linguagem e suas opera√ß√µes, entre todos os tipos b√°sicos o que possu√≠ o maior n√∫mero de opera√ß√µes que normalmente utilizamos √© o m√≥dulo `String`.

```ocaml
String.sub "ocaml4noobs" 5 6;;
(* - : string = "4noobs" *)

String.concat "" ["ocaml"; "4"; "noobs"];;
(* - : string = "ocaml4noobs" *)

String.split_on_char '4' "ocaml4noobs";;
(* - : string list = ["ocaml"; "noobs"] *)
```

## Bool

O tipo `bool` ou  booleano representa valores l√≥gicos, especificamente escritos na forma literal sendo um dos valores `true` ou `false`.

### Operadores Booleanos

As fun√ß√µes respons√°veis por opera√ß√µes booleanas como AND, OR e NOT seguem uma sintaxe que √© familiar para quem j√° programa em outras linguagens.

- **&&**: Fun√ß√£o infixo "E" que recebe dois argumentos do tipo `bool` e retorna `true` caso ambos argumentos sejam do valor `true`.
- **||**: Fun√ß√£o infixo "OU" que recebe dois argumentos do tipo `bool` e retorna `true` caso ao menos um dos argumentos seja `true`.
- **not**: Fun√ß√£o prefixo "N√ÉO" que recebe um argumento do tipo `bool` e retorna o valor booleano oposto ao do argumento recebido.

## Unit

Toda express√£o em OCaml deve ser avaliada para um valor, nos casos em que a express√£o n√£o tenha nenhum resultado l√≥gico poss√≠vel precisa de um tipo que satisfa√ßa essa situa√ß√£o onde temos a falta de um valor e para esse caso √© utilizado o tipo `unit`. Utilizamos a express√£o vazia `()` para escrever o tipo de forma literal, que pode ser usada para declarar uma fun√ß√£o sem retorno, ou uma fun√ß√£o sem par√¢metros por ex.

```ocaml
let hello () =  Printf.printf "Hello World\n";;
(* - : unit = () *)
```

## List

Uma lista √© uma cole√ß√£o de elementos imut√°veis do mesmo tipo, sendo esse tipo qualquer tipo v√°lido em OCaml. Isso significa que em OCaml as listas devem conter necessessariamente elementos do mesmo tipo. 

Como exemplo, se criarmos uma lista que cont√™m n√∫meros inteiros de 1 a 3 `[1; 2; 3]` teremos inferido que essa √© uma lista do tipo `int list` ou seja uma lista de inteiros, se criarmos uma lista de caracteres de 'a' a 'c' `['a'; 'b'; 'c']` vamos ter inferido que o tipo √© `char list` ou seja uma lista de caracteres, mas se tentarmos criar uma lista com caracteres e inteiros como `[1; 'a'; 2; 'b']` teremos um **erro**, pois essa lista n√£o tem um tipo que pode ser inferido.

```ocaml
(* Criando uma lista vazia em OCaml, observe que 'a √© um Generic para qualquer tipo *)
[];;
(* - : 'a list = [] *)

(* Criando uma lista de floats *)
[1.5; 2.; 3.1514];;
(* - : float list = [1.5; 2.; 3.1514] *)
```

Assim como outros valores, listas tamb√©m s√£o imut√°veis uma vez em que s√£o atribuidas, isso significa que n√£o podem ter seus elementos ou seu n√∫mero de elementos alterados, para que possamos alterar uma lista atrav√©s de uma fun√ß√£o √© necess√°rio copiar essa lista e retornar a altera√ß√£o no retorno dessa fun√ß√£o como uma nova lista. Isso √© valido tamb√©m para as opera√ß√µes da biblioteca padr√£o de OCaml, a fun√ß√£o de adicionar um novo elemento a lista ou `head` que utiliza o operador infixo `::`retorna uma nova lista com o elemento concatenado na primeira posi√ß√£o da mesma.

```ocaml
let lista_de_inteiros = [2; 3];;
(* val lista_de_inteiros : int list = [2; 3] *)

1 ::  lista_de_inteiros;;
(* - : int list = [1; 2; 3] *)
```

Em OCaml quando pensamos no tipo `list` podemos pensar em Linked Lists ou listas ligadas simples, onde cada elemento cont√™m um valor e a refer√™ncia do pr√≥ximo item. Isso tem implica√ß√µes importantes de performance, especialmente quando comparamos com `array` ou outra estrutura. Isso por que dependendo a opera√ß√£o que precisamos fazer como por exemplo acessar o √∫ltimo valor implica que teremos que percorrer todos os elementos da lista.

Outra forma de se pensar √© que listas s√£o como conjuntos de elementos que s√£o adicionados at√© uma lista vazia, podemos pensar que:

```ocaml
(* Se definirmos uma lista como *)
let lista_convencional = ['O'; 'C'; 'a'; 'm'; 'l'];;
(* val lista_convencional : char list = ['O'; 'C'; 'a'; 'm'; 'l'] *)

(* ela tamb√©m pode ser vista como *)
let lista = ('O' :: ('C' :: ('a' :: ('m' :: ('l' :: [])))));;
(* val lista : char list = ['O'; 'C'; 'a'; 'm'; 'l'] *)

lista = lista_convencional;;
(* - : bool = true *)
```

###  Combinando Listas

Para unir duas listas utilizamos a fun√ß√£o `List.append` que √© equivalente ao operador de `@` que ir√° receber duas listas e nos retornar uma nova com os elementos da primeira unidos com os elementos da segunda em uma nova lista.

```ocaml
List.append [1;2;3] [4;5;6];;
(* - : int list = [1; 2; 3; 4; 5; 6] *)

[1;2] @ [2;3];;
(* - : int list = [1; 2; 2; 3] *)

['a';'b'] @ ['c';'d'];;
(* - : char list = ['a'; 'b'; 'c'; 'd'] *)
```

Tamb√©m √© poss√≠vel concatenar listas usando a fun√ß√£o `List.concat`.

```ocaml
List.concat [[1;2];[3;4;5];[6];[]];;
(* - : int list = [1; 2; 3; 4; 5; 6] *)
```

## Arrays

O tipo `array` ou matriz em OCaml a principio se parece com `list`, mas esses tipos possuem caracter√≠sticas diferentes de performance e enquanto o tipo `list` √© estruturalmente im√∫tavel, array tem sua estrutura mut√°vel. A sintaxe de matrizes √© a mesma de listas, por√©m al√©m dos colchetes temos tamb√©m pipes delimitando os itens do array.

```ocaml
let numbers = [| 1; 2; 3; 4 |] ;;
(* val numbers : int array = [|1; 2; 3; 4|] *)

numbers.(2) <- 4;;
(* - : unit = () *)

numbers;;
(* - : int array = [|1; 2; 4; 4|] *)
```

A sintaxe `.(i)` √© usada para acessar a refer√™ncia de um elemento em determinada posi√ß√£o do array e `<-` √© a sintaxe para fazer a modifica√ß√£o no elemento. Como matrizes assim como listas s√£o indexadas em zero, o elemento `.(2)` √© o terceiro da lista.

Podemos tamb√©m utilizar a sintaxe `.(i)` para acessar elementos. Diferente de listas n√£o podemos usar o operador de head `::` para adicionar elementos.

## Tuplas

Tuplas s√£o um grupo de valores imut√°veis ordenados, as tuplas s√£o normalmente utilizadas para armazenar resultados intermedi√°rios de c√°lculos, quando precisamos que uma fun√ß√£o retorne m√∫ltiplos valores ou queremos receber pares de valores como par√¢metros. Os valores em uma tupla podem ser de tipos diferentes e seus tipos s√£o escritos em ordem separados por asterisco.

```ocaml
(1, "Camilo", 1.80, true);;
(* - : int * string * float * bool = (1, "Camilo", 1.8, true) *)
```

> Para os amantes de matem√°tica, `*` √© usado para separar os tipos `t * s` porque esse tipo corresponde ao conjunto dos pares que ligam um elemento do conjunto `t` a um elemento do conjunto `s`. Em outras palavras √© um produto cartesiano entre dois tipos, esse √© o motivo que se usa `*`que √© o s√≠mbolo de produto.

Diferente de arrays e listas, n√£o possu√≠mos nenhum √≠ndice em tuplas e por tanto n√£o podemos acessar seus valores a partir de determinada posi√ß√£o. Para fazer isso podemos trabalhar de duas formas.

1. A primeira √© quando temos tuplas com dois elementos, nesse caso podemos utilizar a fun√ß√£o **fst** para acessar a primeira posi√ß√£o e a fun√ß√£o **snd** para acessar a segunda posi√ß√£o. Observe que se tivermos uma tupla com mais que dois elementos como argumento dessa fun√ß√£o isso ir√° nos retornar um erro.

2. Em todos os casos restatnes, utilizamos **Pattern Matching** para acessar os elemntos da tupla.

```ocaml
fst ("ocaml4noobs", 3);;
(* - : string = "ocaml4noobs" *)

snd ("ocaml4noobs", 3);;
(* - : int = 3 *)

let (name, age, height) = ("Camilo", 26, 1.86);;
(* val name : string = "Camilo"
   val age : int = 26
   val height : float = 1.86 *)
```

